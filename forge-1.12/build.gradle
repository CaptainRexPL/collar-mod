buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            url "https://www.cursemaven.com"
        }
        maven {
            name = 'Collar'
            url = 'https://raw.githubusercontent.com/collarmc/maven/main/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = project.property('collar.mod.version') + '-' + getCheckedOutGitCommitHash()
group = 'com.collarmc.mod'
archivesBaseName = 'collar-forge-' + project.property('collar.mc.version')
sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    runDir = "run"
    version = project.property('collar.forge.version')
    mappings = "${project.property('collar.mcp.channel')}_${project.property('collar.mcp.mapping')}"
    makeObfSourceJar = false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    implementation project(':plastic-api')
    implementation project(':common')
    implementation group: 'com.collarmc', name: 'pounce', version: "${project.property('pounce.version')}"
    implementation "com.mojang:brigadier:${project.property("brigadier.version")}"
    implementation 'curse.maven:journeymap-32274:2916002' // journeymap-1.12.2-5.7.1
}

shadowJar {
    dependencies {
        include(dependency('com.mojang:brigadier:.*'))
        include(dependency('com.collarmc:pounce:.*'))
        include(project(':plastic-api'))
        include(project(':common'))
        include(dependency('com.collarmc:client:.*'))
        include(dependency('com.collarmc:plugin:.*'))
    }
    relocate 'com.mojang.brigadier', 'com.collarmc.libs.com.mojang.brigadier'
    relocate 'com.collarmc.shared', 'com.collarmc.libs.com.collarmc.shared'
    relocate 'com.collarmc.client', 'com.collarmc.libs.com.collarmc.client'
    relocate 'com.collarmc.pounce', 'com.collarmc.libs.com.collarmc.pounce'
    classifier ''
}

jar.finalizedBy('shadowJar','reobfJar')